plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.30'
    id 'com.squareup.sqldelight'
}

android {
    compileSdk 31

    lintOptions {
        abortOnError false
    }

    // look in gradle.properties
    def version = prop("version")
    def code = Integer.parseInt(version.split("\\.")[0]) * 100 +
        Integer.parseInt(version.split("\\.")[1]) * 10 +
        Integer.parseInt(version.split("\\.")[2])

    defaultConfig {
        applicationId "berlindroid.zethree"
        minSdk 28
        targetSdk 31
        versionCode code
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file(System.getenv('KEYSTORE_FILE') ?: '../keystore/zeapp-keystore-public')
            storePassword System.getenv('KEYSTORE_PASS') ?: 'android'
            keyAlias System.getenv('KEY_ALIAS') ?: 'android'
            keyPassword System.getenv('KEY_PASS') ?: 'android'
        }
    }
    buildTypes {
        release {
            applicationIdSuffix ".prod"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
        viewBinding true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    def stream_version = "4.20.0"
    implementation "io.getstream:stream-chat-android-compose:$stream_version-beta"

    implementation "com.github.kibotu:AndroidAnimationsActions:2.0.1"

    implementation 'androidx.core:core:1.7.0-rc01'
    implementation "androidx.compose.foundation:foundation:$compose_version"

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation 'androidx.activity:activity:1.4.0-rc01'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0'
    implementation 'io.coil-kt:coil-compose:2.3.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation "com.squareup.sqldelight:android-driver:1.5.2"

    implementation "androidx.work:work-runtime:2.7.0"
    implementation "androidx.work:work-gcm:2.7.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.12.0"
    testImplementation 'app.cash.turbine:turbine:0.6.1'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    debugImplementation "com.github.chuckerteam.chucker:library:3.5.2"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:3.5.2"
}

// from gradle.properties
def prop(String name) {
    return properties[name]?.toString()
}
